package routes/subjects
  use libretto/web/in
  use com/teacode/jetx
  use com/teacode/json
  use com/teacode/json/v2
  use com/teacode/data

  use model
  use utils
  use utils/auth

def createOne( subject: v2/Object! ): Any* = data/w: {
  if( not auth/isAdmin ){
    jetx/out: json"{
      "error": {
        "message": "Access denied"
      }
    }"

    return ()
  }

  fix id = model/saveSubject( subject )

  jetx/out: json"{
    "payload": #{id}
  }"
}

def getAll = data/w: jetx/out: {
  json"{
    "payload": [#{model/getSubjects.*utils/subjectsToJson}]
  }"
}

def home = {
  in/method.{
    case 'POST => createOne( in/bodyText.json/parseObject )
    case 'GET => getAll
  }
}

package routes/subjects/current
  use libretto/web/in
  use com/teacode/jetx
  use com/teacode/json
  use com/teacode/json/v2
  use com/teacode/data

  use model
  use utils/auth

def updateOne( id: Int!, subject: v2/Object! ): Any* = data/w: {
  if( not auth/isAdmin ){
    jetx/out: json"{
      "error": {
        "message": "Access denied"
      }
    }"

    return ()
  }

  model/updateSubject( id, subject )

  jetx/out: json"{
    "payload": null
  }"
}

def deleteOne( id: Int! ): Any* = data/w: {
  if( not auth/isAdmin ){
    jetx/out: json"{
      "error": {
        "message": "Access denied"
      }
    }"

    return ()
  }

  model/deleteSubject( id )

  jetx/out: json"{
    "payload": null
  }"
}

def home = {
  fix id = jetx/params.inte( 'id )

  in/method.{
    case 'PUT => updateOne( id, in/bodyText.json/parseObject )
    case 'DELETE => deleteOne( id )
  }
}